float pow2(float x)
{
    return x*x;
}

vec3 pow2(vec3 x)
{
    return x*x;
}

//
// ref. [Preetham99][Hillaire16]
//
// equivalent with [ray-mmd] ComputeWaveLengthRaylenigh
//
vec3 ComputeCoefficientRayleigh(vec3 lambda)
{
    const float n = 1.0003f; // refractive index
    const float N = 2.545e25f; // molecules per unit
    const float p = 0.035f; // depolarization factor for standard air

    const vec3 l4 = lambda*lambda*lambda*lambda;
    return 8*pi*pi*pi*pow(n*n - 1, 2) / (3*N*l4) * ((6 + 3*p)/(6 - 7*p));
}

// Turbidity: (pure air to thin fog)[Preetham99] A.3
vec3 ComputeCoefficientMie(vec3 lambda, vec3 K, float turbidity)
{
    const float c = max(0.f, 0.6544f*turbidity - 0.6510f)*1e-16f; // concentration factor
    // let jungeexp = 4.0
    const float mie =  0.434f * c * pi * pow2(2*pi);
    // original equation use pow equation, but it has high sensitivy 
    // with turbidity so just use linear
    // return mie * K / pow(lambda, vec3(jungeexp - 2));
    return mie * K / lambda;
}

float ComputePhaseMie(float theta, float g)
{
    return 3.0 / (8.0*pi) * ((1 - g*g)*(1 + theta*theta)) / ((2 + g*g)*pow(1 + g*g - 2 * g*theta, 1.5));
}

float ComputePhaseRayleigh(float theta)
{
    return 3.0 / (16.0*pi) * (1.0 + theta*theta);
}

